subroutine suriwe(kmax      ,kmxdt     ,kmxt      ,h0        ,scale     , &
                & bvav      ,xkh       ,angle     ,omeg      ,singul    , &
                & kbed      ,ktop      ,kcrit     ,wsp10     ,ustwix    , &
                & ustwiy    ,utg       ,vtg       ,frcsur    ,edvis     , &
                & qz        ,r1tg      ,dijdij    ,ttkiw     ,tiwtk     , &
                & az        ,zamp      ,dzdz      ,luniwe    ,gdp       )
!----- GPL ---------------------------------------------------------------------
!                                                                               
!  Copyright (C)  Stichting Deltares, 2011-2012.                                
!                                                                               
!  This program is free software: you can redistribute it and/or modify         
!  it under the terms of the GNU General Public License as published by         
!  the Free Software Foundation version 3.                                      
!                                                                               
!  This program is distributed in the hope that it will be useful,              
!  but WITHOUT ANY WARRANTY; without even the implied warranty of               
!  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the                
!  GNU General Public License for more details.                                 
!                                                                               
!  You should have received a copy of the GNU General Public License            
!  along with this program.  If not, see <http://www.gnu.org/licenses/>.        
!                                                                               
!  contact: delft3d.support@deltares.nl                                         
!  Stichting Deltares                                                           
!  P.O. Box 177                                                                 
!  2600 MH Delft, The Netherlands                                               
!                                                                               
!  All indications and logos of, and references to, "Delft3D" and "Deltares"    
!  are registered trademarks of Stichting Deltares, and remain the property of  
!  Stichting Deltares. All rights reserved.                                     
!                                                                               
!-------------------------------------------------------------------------------
!  $Id: suriwe.f90 1180 2012-01-13 17:05:48Z mourits $
!  $HeadURL: https://svn.oss.deltares.nl/repos/delft3d/tags/5.01.00.2163/src/engines_gpl/flow2d3d/packages/kernel/src/taylor/suriwe.f90 $
!!--description-----------------------------------------------------------------
!
!    Function: Transfer of energy from wind- and surface-wave-
!              generated IW's into TKE.
!              The latter transfer is a distributed TKE pro-
!              duction (TIWTK) according to the IW-imposed
!              shear rate.
!
!              TIWTK is the distributed TKE production in
!                                                    [m^2/s^3].
!
!
!!--pseudo code and references--------------------------------------------------
! NONE
!!--declarations----------------------------------------------------------------
    use precision
    use globaldata
    !
    implicit none
    !
    type(globdat),target :: gdp
    !
    ! The following list of pointer parameters is used to point inside the gdp structure
    !
    real(fp) , pointer :: alfaz
    real(fp) , pointer :: clu
    real(fp) , pointer :: viscof
    logical  , pointer :: iwedia
!
! Global variables
!
    integer         :: kbed
                                   !!  KTG-value of upper level strat. layer
    integer, intent(in)            :: kcrit
                                   !!  K-value on TG grid of the critical
                                   !!  level associated with a given IW own
                                   !!  mode (R1TG)
    integer         :: kmax !  Description and declaration in esm_alloc_int.f90
    integer, intent(in)            :: kmxdt !  Description and declaration in dimens.igs
    integer         :: kmxt !  Description and declaration in dimens.igs
    integer         :: ktop
                                   !!  KTG-value of lower level strat. layer
    integer, intent(in)            :: luniwe
                                   !!  Unit number of IWE diagnostic file
    logical, intent(in)            :: singul
                                   !!  .true. implies lee wave with critical
                                   !!  layer
    real(fp)        :: angle
                                   !!  Angle [degrees] between horizontal
                                   !!  IW wavenumber vector and x-u-axis
    real(fp)        :: az
                                   !!  Rms internal wave vertical displace-
                                   !!  ment amplitude [m]
    real(fp), intent(in)               :: bvav
                                   !!  Mean bouyancy frequency
    real(fp), intent(in)               :: frcsur !  Description and declaration in iwepar.igs
    real(fp)        :: h0
                                   !!  Water depth
    real(fp)        :: omeg
                                   !!  Angular frequency of IW with respect
                                   !!  to ground (root of TG equation)
    real(fp)        :: scale
                                   !!  Length scale for non-dimensio-
                                   !!  nalising
    real(fp), intent(in)               :: ustwix
                                   !!  X-component of wind-shear stress
                                   !!  velocity
    real(fp), intent(in)               :: ustwiy
                                   !!  Y-component of wind-shear stress
                                   !!  velocity
    real(fp), intent(in)               :: wsp10
                                   !!  Wind velocity [m/s]
    real(fp)        :: xkh
                                   !!  Wave number magnitude normalized by
                                   !!  SCALE
    real(fp), dimension(0:kmxdt) :: dijdij
                                   !!  Shear rate induced by lee waves
    real(fp), dimension(0:kmxdt) :: dzdz
                                   !!  Maximal IW-induced vertical strain
                                   !!  rate
    real(fp), dimension(0:kmxdt), intent(in) :: edvis
                                   !!  Interpolated eddy viscosity
    real(fp), dimension(0:kmxdt) :: qz
                                   !!  Vertical wave number squared, in TG
                                   !!  equation
    real(fp), dimension(0:kmxdt) :: r1tg
    real(fp), dimension(0:kmxdt) :: tiwtk
                                   !!  Transfer rate of IWE to TKE
    real(fp), dimension(0:kmxdt) :: ttkiw
                                   !!  Transfer rate of TKE to IWE
    real(fp), dimension(0:kmxdt) :: utg
    real(fp), dimension(0:kmxdt) :: vtg
                                   !!  v-velocity component on TG grid
    real(fp), dimension(0:kmxdt) :: zamp
                                   !!  Maximal vertical displacement amp-
                                   !!  litude of bed-induced IW
!
!
! Local variables
!
    integer                        :: k
    integer                        :: kmn                  ! Lower KTG-value for analysis 
    integer                        :: kmx                  ! Upper KTG-value for analysis 
    real(fp)                       :: aw                   ! Rms internal wave vertical velocity amplitude [m/s] 
    real(fp)                       :: crad
    real(fp)                       :: d2rdz2
    real(fp)                       :: dirsur
    real(fp)                       :: dz
    real(fp)                       :: dztg
    real(fp)                       :: dztg2
    real(fp)                       :: epsav                ! Depth-averaged dissipation of lee waves, after amplitude reduction 
    real(fp)                       :: epsscl               ! Scaling factor for dissipation/ production terms [m^2/s^3] 
    real(fp)                       :: excit                ! Excitation rate [m^3/s^3] of IW's generated by surface perturbations 
    real(fp)                       :: fac
    real(fp)                       :: frccor               ! Reduced FRCSUR 
    real(fp)                       :: ha2w                 ! 0.5*AW^2, calibrated variance of vertical IW velocity 
    real(fp)                       :: power                ! Total power spent by lee waves wor- king against viscous and turbulent flow 
    real(fp)                       :: reduc
    real(fp)                       :: sumtit               ! Depth integral of TKE production by IW shear rates DIJDIJ 
    real(fp)                       :: tit                  ! Distributed TKE production by IW shear rates [m^2/s^3] 
    real(fp)                       :: u2wx
    real(fp)                       :: u2wy
    real(fp)                       :: ustw2                ! Magnitude of wind-shear stress vector 
    real(fp)                       :: velscl               ! Velocity scale for non-dimensionaling 
    real(fp)                       :: vismol               ! Non-dimensional kinematic viscosity 
    real(fp)                       :: visscl               ! Viscosity scale for non-dimensio- naling 
    real(fp)                       :: xkl2
    real(fp)                       :: xmu
    real(fp)                       :: zcrit
!
!
!! executable statements -------------------------------------------------------
!
    !
    !
    alfaz   => gdp%gdiwearr%alfaz
    clu     => gdp%gdiwearr%clu
    viscof  => gdp%gdiwearr%viscof
    iwedia  => gdp%gdiwearr%iwedia
    !
    dz = h0/kmxt
    dztg = dz/scale
    dztg2 = dztg**2
    xkl2 = xkh**2
    !
    epsscl = (bvav**3)*(scale**2)
    visscl = bvav*(scale**2)
    velscl = bvav*scale
    crad = atan(1.)/45.
    !
    vismol = viscof/visscl
    !
    u2wx = ustwix*abs(ustwix)
    u2wy = ustwiy*abs(ustwiy)
    ustw2 = sqrt(u2wx**2 + u2wy**2)
    dirsur = acos(u2wx/ustw2)/crad
    if (u2wy<0.0) dirsur = 360. - dirsur
    !
    ! Direction from which the wind blows:
    !
    if (dirsur<180.) then
       dirsur = dirsur + 180.
    else
       dirsur = dirsur - 180.
    endif
    !
    ! Estimate depth-integrated power, EXCIT [m^3/s^3],
    ! transferred by the wind to the flow:
    !
    excit = ustw2*wsp10/(epsscl*scale)
    !
    ! This routine is called only for top=.true.:
    !
    kmn = 0
    kmx = kmxt
    if (singul) kmx = kcrit
    !
    ! Second-order derivative of eigen-solution d^2r/dz^2 is "d2rdz2":
    !
    power = 0.0
    do k = kmn + 1, kmx - 1
       if (abs(qz(k))>1.E-6) then
          d2rdz2 = -qz(k)*r1tg(k)
       else
          d2rdz2 = (r1tg(k - 1) + r1tg(k + 1) - 2*r1tg(k))/dztg2
       endif
       dijdij(k) = d2rdz2**2/xkl2
       power = power + (vismol + edvis(k))*dijdij(k)
    enddo
    power = power*dztg
    !
    ! Fraction of the depth-integrated power spent by the flow on
    ! bed-induced stresses that is converted into IWE:
    !
    sumtit = frcsur*excit
    !
    ! Calibrate IW variance 0.5*(A_w)^2 for matching the energy flux
    ! from TKE to IWE to the energy flux from TKE to IWE, estimated
    ! above:
    !
    ha2w = sumtit/power
    !
    ! A_w=aw is the rms vertical velocity amplitude in [m/s].
    ! A_z=az is the rms wave amplitude [m], to be multiplied by R1TG:
    !
    aw = sqrt(ha2w)*velscl
    az = aw/bvav
    frccor = frcsur
    !
    ! If maximal IW amplitude exceeded then power reduction:
    !
    if (.not.singul) then
       call ampiwe(angle     ,az        ,h0        ,kbed      ,ktop      , &
                 & kmxt      ,omeg      ,qz        ,r1tg      ,scale     , &
                 & utg       ,vtg       ,xkh       ,zamp      ,dzdz      , &
                 & gdp       )
       !
       !
       xmu = alfaz*sqrt(2.)
       if (dzdz(kmxdt)>xmu) then
          reduc = xmu/dzdz(kmxdt)
          dzdz(kmxdt) = xmu
          az = reduc*az
          aw = bvav*az
          ha2w = (aw/velscl)**2
          sumtit = power*ha2w
          frccor = sumtit/excit
          zamp(kmxdt) = reduc*zamp(kmxdt)
       endif
    endif
    epsav = sumtit*epsscl*scale
    !
    ! Transfer from IW to turbulence according to IW-induced stress
    ! power and convert into physical units [m^2/s^3].
    !
    ! Notice that in POWER (do-loop 10) molecular viscosity is included.
    ! However, just the work of the IW's against turbulence produces
    ! turbulence, thus TIWTK equals EDVIS*DijDij !!
    !
    sumtit = sumtit*epsscl*scale
    fac = ha2w*epsscl
    !
    do k = kmn + 1, kmx - 1
       tit = fac*edvis(k)*dijdij(k)
       tiwtk(k) = tiwtk(k) + tit
    enddo
    !
    ! Wave-current forces:
    !
    !
    ! Diagnostics:
    !
    if (iwedia) then
       !
       write (luniwe, *)
       write (luniwe, '(a)') ' SURIWE                          :'
       write (luniwe, '(a,1x,g8.3)') ' Wind direction             [dgr]:',      &
                                   & dirsur
       write (luniwe, '(a,1x,g8.3)') ' Angle of IW propagation    [dgr]:', angle
       write (luniwe, '(a,1x,g8.2)') ' Power transfer to TKE  [m^3/s^3]:', epsav
       write (luniwe, '(a,1x,g8.2)') ' Fraction of power of flow    [-]:',      &
                                   & frccor
       write (luniwe, '(a,1x,g8.2)') ' Near-surface orb. velocity [m/s]:', aw
       !
       write (luniwe, '(a,1x,g8.2)') ' Max dZ/dz in strat. layer    [-]:',      &
                                   & dzdz(kmxdt)
       write (luniwe, '(a,1x,g8.2)') ' Max. z-displacement          [m]:',      &
                                   & zamp(kmxdt)
       if (singul) then
          zcrit = (kmxt - kcrit)*h0/kmxt
          write (luniwe, '(a,1x,g8.2)') ' Level crit. layer above bed  [m]:',   &
                                      & zcrit
       else
          write (luniwe, '(a)') ' No critical layer               :'
       endif
    endif
end subroutine suriwe
