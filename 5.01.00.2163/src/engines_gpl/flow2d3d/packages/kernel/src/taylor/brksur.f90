subroutine brksur(kmxdt     ,kmxt      ,nfreqs    ,angle     ,atg       , &
                & btg       ,bv2       ,ctg       ,d2u       ,d2v       , &
                & h0        ,kbed      ,ktop      ,qz        ,r1tg      , &
                & scale     ,tgfind    ,utg       ,vtg       ,xkh       , &
                & ustwix    ,ustwiy    ,frlo      ,frup      ,bvmx      , &
                & luniwe    ,gdp       )
!----- GPL ---------------------------------------------------------------------
!                                                                               
!  Copyright (C)  Stichting Deltares, 2011-2012.                                
!                                                                               
!  This program is free software: you can redistribute it and/or modify         
!  it under the terms of the GNU General Public License as published by         
!  the Free Software Foundation version 3.                                      
!                                                                               
!  This program is distributed in the hope that it will be useful,              
!  but WITHOUT ANY WARRANTY; without even the implied warranty of               
!  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the                
!  GNU General Public License for more details.                                 
!                                                                               
!  You should have received a copy of the GNU General Public License            
!  along with this program.  If not, see <http://www.gnu.org/licenses/>.        
!                                                                               
!  contact: delft3d.support@deltares.nl                                         
!  Stichting Deltares                                                           
!  P.O. Box 177                                                                 
!  2600 MH Delft, The Netherlands                                               
!                                                                               
!  All indications and logos of, and references to, "Delft3D" and "Deltares"    
!  are registered trademarks of Stichting Deltares, and remain the property of  
!  Stichting Deltares. All rights reserved.                                     
!                                                                               
!-------------------------------------------------------------------------------
!  $Id: brksur.f90 1180 2012-01-13 17:05:48Z mourits $
!  $HeadURL: https://svn.oss.deltares.nl/repos/delft3d/tags/5.01.00.2163/src/engines_gpl/flow2d3d/packages/kernel/src/taylor/brksur.f90 $
!!--description-----------------------------------------------------------------
!
!    Function: Bracketing largest angluar fequency OMEG of TG
!              equation, for finding representative eigen mode
!              of IW's generated by surface perturbations.
!
!
!!--pseudo code and references--------------------------------------------------
! NONE
!!--declarations----------------------------------------------------------------
    use precision
    use globaldata
    !
    implicit none
    !
    type(globdat),target :: gdp
    !
    ! The following list of pointer parameters is used to point inside the gdp structure
    !
    logical , pointer :: iwedia
!
! Global variables
!
    integer         :: kbed
                                   !!  K-value on TG grid: upper level
                                   !!  stratified layer
    integer         :: kmxdt !  Description and declaration in dimens.igs
    integer         :: kmxt !  Description and declaration in dimens.igs
    integer         :: ktop
                                   !!  K-value on TG grid: lower level
                                   !!  stratified layer
    integer, intent(in)            :: luniwe
                                   !!  Unit number for diagnostic reports of
                                   !!  this subprogram as well as called
                                   !!  subroutines
    integer, intent(in)            :: nfreqs !  Description and declaration in dimens.igs
    logical         :: tgfind
                                   !!  Indicator for instructing subroutine
                                   !!  TG to yield the eigen mode for given
                                   !!  input
    real(fp)        :: angle
                                   !!  Angle [degrees] between horizontal
                                   !!  IW wavenumber vector and x-u-axis
    real(fp), intent(in)               :: bvmx
                                   !!  Maximal buoyancy frequency
    real(fp)        :: frlo
                                   !!  Left bracket of frequency interval
                                   !!  with largest root of TG equation
    real(fp)        :: frup
                                   !!  Right bracket of frequency interval
                                   !!  with largest root of TG equation
    real(fp)        :: h0
                                   !!  Water depth
    real(fp)        :: scale
                                   !!  Length scale for scaling
    real(fp), intent(in)               :: ustwix
                                   !!  X/u-component of wind-shear stress
                                   !!  velocity
    real(fp), intent(in)               :: ustwiy
                                   !!  Y/v-component of wind-shear stress
                                   !!  velocity
    real(fp)        :: xkh
                                   !!  Non-dimensional XK
    real(fp), dimension(0:kmxdt) :: atg
    real(fp), dimension(0:kmxdt) :: btg
    real(fp), dimension(0:kmxdt) :: bv2
    real(fp), dimension(0:kmxdt) :: ctg
    real(fp), dimension(0:kmxdt) :: d2u
    real(fp), dimension(0:kmxdt) :: d2v
    real(fp), dimension(0:kmxdt) :: qz
    real(fp), dimension(0:kmxdt) :: r1tg
    real(fp), dimension(0:kmxdt) :: utg
    real(fp), dimension(0:kmxdt) :: vtg
!
!
! Local variables
!
    integer                        :: i
    logical                        :: top
    real(fp)                       :: crit
    real(fp)                       :: delf
    real(fp)                       :: dirwi
    real(fp)                       :: flo
    real(fp)                       :: fup
    real(fp)                       :: omeg
    real(fp)                       :: u2wx
    real(fp)                       :: u2wy
    real(fp)                       :: ustw2
!
!
!! executable statements -------------------------------------------------------
!
    !
    !
    iwedia  => gdp%gdiwearr%iwedia
    !
    u2wx = ustwix*abs(ustwix)
    u2wy = ustwiy*abs(ustwiy)
    ustw2 = sqrt(u2wx**2 + u2wy**2)
    if (ustw2<1.E-12) then
       dirwi = 0.
    else
       dirwi = 45.*acos(u2wx/ustw2)/atan(1.)
    endif
    if (u2wy<0.0) dirwi = 180. + dirwi
    !
    ! Orientation of surface-generated IW's is practically
    ! perpendicular to wind-stress direction:
    !
    angle = dirwi + 90.
    if (angle>360.) angle = angle - 360.
    !
    ! Inspect a single range of normalized eigen frequencies:
    ! [0,bvmx] starting from maximal angular freq. FRUP:
    !
    delf = bvmx/nfreqs
    tgfind = .false.
    i = nfreqs
    frlo = frup
    !
    omeg = frlo
    top = .true.
    !
    call tg(kmxdt     ,kmxt      ,angle     ,atg       ,btg       , &
          & bv2       ,ctg       ,crit      ,d2u       ,d2v       , &
          & h0        ,kbed      ,ktop      ,omeg      ,qz        , &
          & r1tg      ,scale     ,tgfind    ,top       ,utg       , &
          & vtg       ,xkh       ,gdp       )
    !
    !
    flo = crit
    !
   10 continue
    i = i - 1
    fup = flo
    frup = frlo
    !
    frlo = frup - delf
    omeg = frlo
    top = .true.
    !
    call tg(kmxdt     ,kmxt      ,angle     ,atg       ,btg       , &
          & bv2       ,ctg       ,crit      ,d2u       ,d2v       , &
          & h0        ,kbed      ,ktop      ,omeg      ,qz        , &
          & r1tg      ,scale     ,tgfind    ,top       ,utg       , &
          & vtg       ,xkh       ,gdp       )
    !
    !
    flo = crit
    !
    if (fup*flo>0. .and. i>0) goto 10
    if (i==0 .and. iwedia) then
       write (luniwe, '(a)')                                                    &
                           & ' BRKSUR                          : no IW"s found ?'
    else
       tgfind = .true.
    endif
end subroutine brksur
